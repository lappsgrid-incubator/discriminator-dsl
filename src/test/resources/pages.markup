/*
 * Template used to generate a single HTML page for a discriminator.
 */
html {
    head {
        title "${page.name}"
        link rel:'stylesheet', type:'text/css', href:'http://vocab.lappsgrid.org/discriminators.css'
    }
    body(id:'container') {
        div(id:'intro') {
            div(id:'pageHeader') {
                h1 'LAPPS Web Service URI Inventory'
                h2 "${page.name}"
            }
        }
        div(id:'mainContent') {
            //h1 "${page.name}"
            p class:'note', 'This information is posted for comment and discussion only.'
            p class:'note', 'This file is machine generated and should not be edited.'
            def discriminators = page.discriminators
            def first = discriminators[0]
            h2 first.discriminator.uri
            p first.discriminator.description

            if (discriminators.size() > 1) {
                ul {
                    discriminators[1..-1].each { d ->
                        li {
                            a(href:d.fragment, d.fragment.substring(1))
                        }
                    }
                }
                discriminators[1..-1].each { d ->
                    a name:"${d.fragment.substring(1)}", ''
                    h2 d.discriminator.uri
                    p d.discriminator.description
                }
            }
        }
        div(class:'footer') {
            def year = Calendar.getInstance().get(Calendar.YEAR)
            mkp.yieldUnescaped "<p>Copyright &copy; ${year} The Language Application Grid. All rights reserved.</p>"
        }
    }
}